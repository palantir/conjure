/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.palantir.graal' version '0.7.2'
}

apply from: "$rootDir/gradle/publish-dist.gradle"

mainClassName = 'com.palantir.conjure.cli.ConjureCli'

dependencies {
    compile project(':conjure-core')
    compile 'commons-io:commons-io'
    compile 'info.picocli:picocli'
    runtime 'org.slf4j:slf4j-simple'

    testCompile 'junit:junit'
    testCompile 'org.assertj:assertj-core'

    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'
    compileOnly 'org.graalvm.sdk:graal-sdk'
}


graal {
    mainClass mainClassName
    javaVersion '11'
    outputName 'conjure'
    graalVersion '20.3.0'
    // resourceconfig.json which includes all resources to match hotspot.
    option ('-H:ResourceConfigurationFiles=' + file('resourceconfig.json').absolutePath)
    // Fail if a native image cannot be created. Otherwise a fallback image which relies
    // on a hotspot jdk is required.
    option '--no-fallback'
    option '--static'
    // Missing classes result in runtime failures rather than compile time. Many libraries
    // have optional dependencies that may be excluded. Take the log4j-core disruptor
    // dependency for example.
    option '--allow-incomplete-classpath'
    // Allows optional dependencies to fail at runtime, not build time. This feature is
    // equivalent to 'allow-incomplete-classpath' for unsupported java features,
    // methodHandles for example.
    option '--report-unsupported-elements-at-runtime'
    // Easier debugging on failure
    option '-H:+ReportExceptionStackTraces'
    // Discover reflection info at build time
    option '--features=com.palantir.conjure.cli.RuntimeReflectionRegistrationFeature'
}
